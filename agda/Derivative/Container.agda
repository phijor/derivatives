{-# OPTIONS --safe #-}
module Derivative.Container where

open import Derivative.Prelude
import      Derivative.Basics.Maybe as Maybe
open import Derivative.Basics.Sum as Sum using (_âŠ_)

open import Cubical.Foundations.Transport using (substEquiv)
open import Cubical.Reflection.RecordEquiv
open import Cubical.Data.Sigma.Base

record Container (â„“S â„“P : Level) : Type (â„“-suc (â„“-max â„“S â„“P)) where
  -- no-eta-equality
  constructor _â—_
  field
    Shape : Type â„“S
    Pos : Shape â†’ Type â„“P

private
  variable
    â„“S â„“P â„“ : Level

Container-syntax : (S : Type â„“S) â†’ (P : S â†’ Type â„“P) â†’ Container â„“S â„“P
Container-syntax = _â—_

syntax Container-syntax S (Î» s â†’ P) = [ s âˆˆ S ]â— P

open Container

isTruncatedContainer : (n k : HLevel) â†’ Container â„“S â„“P â†’ Type _
isTruncatedContainer n k (S â— P) = isOfHLevel n S Ã— âˆ€ s â†’ isOfHLevel k (P s)

isSetContainer : Container â„“S â„“P â†’ Type _
isSetContainer = isTruncatedContainer 2 2

TruncatedContainer : (n k : HLevel) (â„“S â„“P : Level) â†’ Type (â„“-suc (â„“-max â„“S â„“P))
TruncatedContainer n k â„“S â„“P = Î£ (Container â„“S â„“P) (isTruncatedContainer n k)

SetContainer : (â„“S â„“P : Level) â†’ Type (â„“-suc (â„“-max â„“S â„“P))
SetContainer = TruncatedContainer 2 2

_âŠ—_ : (F G : Container â„“S â„“P) â†’ Container _ _
(F âŠ— G) .Shape = F .Shape Ã— G .Shape
(F âŠ— G) .Pos x = F .Pos (x .fst) âŠ G .Pos (x .snd)
infix 11 _âŠ—_

Id : Container â„“S â„“P
Id .Shape = ğŸ™*
Id .Pos = const ğŸ™*

_âŠ—Id : Container â„“S â„“P â†’ Container â„“S â„“P
F âŠ—Id = F âŠ— Id

_âŠ•_ : (F G : Container â„“S â„“P) â†’ Container _ _
(F âŠ• G) .Shape = F .Shape âŠ G .Shape
(F âŠ• G) .Pos (Sum.inl s) = F .Pos s
(F âŠ• G) .Pos (Sum.inr t) = G .Pos t
infix 10 _âŠ•_

Const : (S : Type â„“) â†’ Container â„“ â„“
Const S .Shape = S
Const S .Pos _ = ğŸ˜*

One : (â„“ : Level) â†’ Container â„“ â„“
One â„“ = Const ğŸ™*

record Cart {â„“S â„“T â„“P â„“Q} (F : Container â„“S â„“P) (G : Container â„“T â„“Q) : Type (â„“-max (â„“-max â„“S â„“P) (â„“-max â„“T â„“Q)) where
  constructor [_â—_]
  field
    shape : F .Shape â†’ G .Shape
    pos : âˆ€ s â†’ G .Pos (shape s) â‰ƒ F .Pos s

open Cart

unquoteDecl Cart-Î£-Iso = declareRecordIsoÎ£ Cart-Î£-Iso (quote Cart)

Cart-Î£-equiv : âˆ€ {â„“S â„“T â„“P â„“Q} {F : Container â„“S â„“P} {G : Container â„“T â„“Q}
  â†’ Cart F G â‰ƒ (Î£[ fâ‚›â‚• âˆˆ (F .Shape â†’ G .Shape) ] âˆ€ s â†’ G .Pos (fâ‚›â‚• s) â‰ƒ F .Pos s)
Cart-Î£-equiv = isoToEquiv Cart-Î£-Iso

Cartâ‰¡ : {F G : Container â„“S â„“P}
  â†’ {f g : Cart F G}
  â†’ (p : f .shape â‰¡ g .shape)
  â†’ (q : PathP (Î» i â†’ âˆ€ s â†’ G .Pos (p i s) â‰ƒ F .Pos s) (f .pos) (g .pos))
  â†’ f â‰¡ g
Cartâ‰¡ p q i .shape = p i
Cartâ‰¡ p q i .pos = q i

Cartâ‰¡Equiv : {F G : Container â„“S â„“P}
  â†’ (f g : Cart F G)
  â†’ (Î£[ p âˆˆ f .shape â‰¡ g .shape ] (PathP (Î» i â†’ âˆ€ s â†’ G .Pos (p i s) â‰ƒ F .Pos s) (f .pos) (g .pos))) â‰ƒ (f â‰¡ g)
Cartâ‰¡Equiv f g = strictIsoToEquiv iso module Cartâ‰¡Equiv where
  iso : Iso _ _
  iso .Iso.fun = uncurry Cartâ‰¡
  iso .Iso.inv p .fst = cong shape p
  iso .Iso.inv p .snd = cong pos p
  iso .Iso.rightInv _ = refl
  iso .Iso.leftInv _ = refl

_â‹†_ : âˆ€ {F G H : Container â„“S â„“P} â†’ Cart F G â†’ Cart G H â†’ Cart F H
(f â‹† g) .shape = g .shape âˆ˜ f .shape
(f â‹† g) .pos s = g .pos (f .shape s) âˆ™â‚‘ f .pos s
-- {-# INJECTIVE_FOR_INFERENCE _â‹†_ #-}

id : (F : Container â„“S â„“P) â†’ Cart F F
id F .shape s = s
id F .pos s = idEquiv _

TruncatedCart : {n k : HLevel} â†’ (F G : TruncatedContainer n k â„“S â„“P) â†’ Type _
TruncatedCart F G = Cart (F .fst) (G .fst)
{-# INJECTIVE_FOR_INFERENCE TruncatedCart #-}

SetCart : (F G : TruncatedContainer 2 2 â„“S â„“P) â†’ Type _
SetCart = TruncatedCart {n = 2} {k = 2}

isOfHLevelCart : (n : HLevel) â†’ {F G : TruncatedContainer n n â„“S â„“P} â†’ isOfHLevel n (TruncatedCart F G)
isOfHLevelCart n {F = _ , _ , trunc-pos-F} {G = _ , trunc-shape-G , trunc-pos-G} = isOfHLevelRetractFromIso n Cart-Î£-Iso
  $ isOfHLevelÎ£ n
    (isOfHLevelÎ  n Î» _ â†’ trunc-shape-G)
    Î» f â†’ isOfHLevelÎ  n Î» s â†’ isOfHLevelâ‰ƒ n (trunc-pos-G (f s)) (trunc-pos-F s)

module CartReasoning where
  private
    variable
      F G H : Container â„“S â„“P

  _âŠ¸âŸ¨_âŸ©_ : (F : Container â„“S â„“P) {G H : Container â„“S â„“P} â†’ Cart F G â†’ Cart G H â†’ Cart F H
  _âŠ¸âŸ¨_âŸ©_ F {G} {H} f g = _â‹†_ {F = F} {G = G} {H = H} f g

  _âŠ¸âˆ : (F : Container â„“S â„“P) â†’ Cart F F
  F âŠ¸âˆ = id F
  {-# INLINE _âŠ¸âˆ #-}

  infixr 0 _âŠ¸âŸ¨_âŸ©_
  infix 1 _âŠ¸âˆ

open CartReasoning public

[_]âŠ—Id : {F G : Container â„“S â„“P} â†’ Cart F G â†’ Cart (F âŠ—Id) (G âŠ—Id)
[_]âŠ—Id {F} {G} f .shape s = f .shape (s .fst) , _
[_]âŠ—Id {F} {G} f .pos s = Sum.âŠ-left-â‰ƒ (f .pos (s .fst))

record Equiv (F G : Container â„“S â„“P) : Type (â„“-max â„“S â„“P) where
  constructor [_â—â‰ƒ_]
  field
    shape : F .Shape â‰ƒ G .Shape
    pos : âˆ€ s â†’ G .Pos (equivFun shape s) â‰ƒ F .Pos s

Equivâ†’Cart : {F G : Container â„“S â„“P} â†’ Equiv F G â†’ Cart F G
Equivâ†’Cart [ shape â—â‰ƒ pos ] .shape = equivFun shape
Equivâ†’Cart [ shape â—â‰ƒ pos ] .pos = pos

_â‹†â‚‘_ : âˆ€ {F G H : Container â„“S â„“P} â†’ Equiv F G â†’ Equiv G H â†’ Equiv F H
(f â‹†â‚‘ g) .Equiv.shape = f .Equiv.shape âˆ™â‚‘ g .Equiv.shape
(f â‹†â‚‘ g) .Equiv.pos s = g .Equiv.pos (equivFun (f .Equiv.shape) s) âˆ™â‚‘ f .Equiv.pos s

idâ‚‘ : (F : Container â„“S â„“P) â†’ Equiv F F
idâ‚‘ F .Equiv.shape = idEquiv _
idâ‚‘ F .Equiv.pos _ = idEquiv _

-- TODO: Rename to Equiv-shape and Equiv-pos
Equiv-fst : âˆ€ {S Sâ€² : Type â„“S} {P : Sâ€² â†’ Type â„“P}
  â†’ (e : S â‰ƒ Sâ€²)
  â†’ Equiv (S â— (P âˆ˜ equivFun e)) (Sâ€² â— P)
Equiv-fst {S} {P} e .Equiv.shape = e
Equiv-fst {S} {P} e .Equiv.pos s = idEquiv (P (equivFun e s))

Equiv-snd : âˆ€ {S : Type â„“S} {P Pâ€² : S â†’ Type â„“P}
  â†’ (e : âˆ€ s â†’ Pâ€² s â‰ƒ P s)
  â†’ Equiv (S â— P) (S â— Pâ€²)
Equiv-snd {S} e .Equiv.shape = idEquiv S
Equiv-snd {S} e .Equiv.pos = e

Equiv-inv : {F G : Container â„“S â„“} â†’ Equiv F G â†’ Equiv G F
Equiv-inv {F} {G} [ shape â—â‰ƒ pos ] .Equiv.shape = invEquiv shape
Equiv-inv {F} {G} [ shape â—â‰ƒ pos ] .Equiv.pos t = invEquiv $ substEquiv (G .Pos) (sym (secEq shape t)) âˆ™â‚‘ pos (invEq shape t)

module EquivReasoning where
  private
    variable
      F G H : Container â„“S â„“P

  _âŠ¸â‰ƒâŸ¨_âŸ©_ : (F : Container â„“S â„“P) â†’ Equiv F G â†’ Equiv G H â†’ Equiv F H
  _âŠ¸â‰ƒâŸ¨_âŸ©_ {G} {H} F f g = _â‹†â‚‘_ {F = F} {G = G} {H = H} f g

  _âŠ¸â‰ƒâˆ : (F : Container â„“S â„“P) â†’ Equiv F F
  F âŠ¸â‰ƒâˆ = idâ‚‘ F
  {-# INLINE _âŠ¸â‰ƒâˆ #-}

  _âŠ¸â‰ƒâŸ¨âŸ©_ : (F : Container â„“S â„“P) â†’ Equiv F G â†’ Equiv F G
  F âŠ¸â‰ƒâŸ¨âŸ© e = e

  infixr 0 _âŠ¸â‰ƒâŸ¨_âŸ©_
  infixr 0 _âŠ¸â‰ƒâŸ¨âŸ©_
  infix 1 _âŠ¸â‰ƒâˆ

open EquivReasoning public

_[_] : (F G : Container â„“ â„“) â†’ Container â„“ â„“
(F [ G ]) .Shape = Î£[ s âˆˆ F .Shape ] (F .Pos s â†’ G .Shape)
(F [ G ]) .Pos (s , f) = Î£[ p âˆˆ F .Pos s ] G .Pos (f p)
