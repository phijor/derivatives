module Derivative.Bag where

open import Derivative.Prelude
open import Derivative.Container
open import Derivative.Derivative
open import Derivative.Isolated
open import Derivative.Remove
open import Derivative.Decidable
open import Derivative.Maybe

open import Cubical.Foundations.Univalence
open import Cubical.Relation.Nullary using (isPropÂ¬)
open import Cubical.Data.Sigma
open import Cubical.Data.Sum as Sum using (inl ; inr)
open import Cubical.Data.FinSet as FinSet renaming (FinSet to FinSet*)
open import Cubical.Data.FinSet.Induction as Fin
open import Cubical.Data.FinSet.Constructors

private
  FinSet = FinSet* â„“-zero

Bag : Container (â„“-suc â„“-zero) â„“-zero
Bag .Container.Shape = FinSet
Bag .Container.Pos = âŸ¨_âŸ©

-- X âˆ– x is a decidable subtype of X, hence a finite set
isFinSetMinus : âˆ€ {X : Type} â†’ isFinSet X â†’ âˆ€ x â†’ isFinSet (X âˆ– x)
isFinSetMinus {X} is-finset-X x = isFinSetÎ£ (X , is-finset-X) Î» xâ€² â†’ (Â¬ x â‰¡ xâ€²) , is-finset-â‰¢ xâ€²
  where
    is-finset-â‰¢ : âˆ€ xâ€² â†’ isFinSet (x â‰¢ xâ€²)
    is-finset-â‰¢ xâ€² = isDecPropâ†’isFinSet (isPropÂ¬ _) (decNot (isFinSetâ†’Discrete is-finset-X x xâ€²))

_-_ : (X : FinSet) â†’ (x : âŸ¨ X âŸ©) â†’ FinSet
(X - x) .fst = âŸ¨ X âŸ© âˆ– x
(X - x) .snd = isFinSetMinus (str X) x

IsolatedFinEquiv : (X : FinSet) â†’ âŸ¨ X âŸ© Â° â‰ƒ âŸ¨ X âŸ©
IsolatedFinEquiv (X , is-finset) = Discreteâ†’IsolatedEquiv $ isFinSetâ†’Discrete {A = X} is-finset

isIsolatedFin : âˆ€ {X : FinSet} (xâ‚€ : âŸ¨ X âŸ©) â†’ isIsolated xâ‚€
isIsolatedFin {X} = Discreteâ†’isIsolated (isFinSetâ†’Discrete (str X))

âˆ‚-shape-equiv : (Î£[ X âˆˆ FinSet ] (âŸ¨ X âŸ© Â°)) â‰ƒ FinSet
âˆ‚-shape-equiv =
  Î£[ X âˆˆ FinSet ] âŸ¨ X âŸ© Â°
    â‰ƒâŸ¨ Î£-cong-equiv-snd IsolatedFinEquiv âŸ©
  Î£[ X âˆˆ FinSet ] âŸ¨ X âŸ©
    â‰ƒâŸ¨ isoToEquiv pred-iso âŸ©
  FinSet
    â‰ƒâˆ
    where
      pred : Î£ FinSet âŸ¨_âŸ© â†’ FinSet
      pred (X , x) = X - x

      suc : FinSet â†’ Î£ FinSet âŸ¨_âŸ©
      suc X .fst = X Fin.+ ğŸ™
      suc X .snd = nothing

      pred-iso : Iso (Î£ FinSet âŸ¨_âŸ©) FinSet
      pred-iso .Iso.fun = pred
      pred-iso .Iso.inv = suc
      pred-iso .Iso.rightInv X = equivFun (FinSetâ‰¡ _ _) $ ua $ removeNothingEquiv
      pred-iso .Iso.leftInv (X , xâ‚€) = Î£PathP Î» where
          .fst â†’ fin-path
          .snd â†’ pt-path
        where
          fin-equiv : âŸ¨ (X - xâ‚€) Fin.+ ğŸ™ âŸ© â‰ƒ âŸ¨ X âŸ©
          fin-equiv = invEquiv $ replace-isolated-equiv xâ‚€ (isIsolatedFin {X = X} xâ‚€)

          fin-path : (X - xâ‚€) Fin.+ ğŸ™ â‰¡ X
          fin-path = equivFun (FinSetâ‰¡ _ _) $ ua fin-equiv

          pt-path : PathP (Î» i â†’ âŸ¨ fin-path i âŸ©) nothing xâ‚€
          pt-path = ua-gluePath fin-equiv $ reflâ€² xâ‚€

âˆ‚-pos-equiv : (X : FinSet) (x : âŸ¨ X âŸ© Â°) â†’ (âŸ¨ X âŸ© âˆ– (x .fst)) â‰ƒ âŸ¨ X - (x .fst) âŸ©
âˆ‚-pos-equiv X x = idEquiv _

âˆ‚-Bag-map : Equiv (âˆ‚ Bag) Bag
âˆ‚-Bag-map .Equiv.shape = âˆ‚-shape-equiv
âˆ‚-Bag-map .Equiv.pos = uncurry âˆ‚-pos-equiv
